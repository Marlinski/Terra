plugins {
    id("java-library")
    id "com.github.harbby.gradle.serviceloader" version "1.1.5"
}

task ApiDoc(type: Javadoc) {
    source = sourceSets.main.allJava
}

jar {
    // META-INF/services to load module with ServiceLoader
    serviceLoader {
        serviceInterface 'io.left.rightmesh.libdtn.core.spi.core.CoreModuleSPI'
        serviceInterface 'io.left.rightmesh.module.aa.hello.CoreModuleHello'
    }

    // fat jar dependency, exclude those already in libdtn-core
    from {
        configurations.compile
                .filter({! it.name.matches("rxjava.*")})
                .filter({! it.name.matches("librxbus.*")})
                .filter({! it.name.matches("reactive-streams.*")})
                .filter({! it.name.matches("libcbor.*")})
                .filter({! it.name.matches("libdtn-common.*")})
                .collect { it.isDirectory() ? it : zipTree(it) }
    }
}

repositories {
    google()
    jcenter()
    maven { url 'https://jitpack.io' }
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // RxBus for the event bus
    implementation 'com.github.RightMesh:librxbus:-SNAPSHOT'

    // CBOR for encoding/parsing
    implementation 'com.github.RightMesh:libcbor:-SNAPSHOT'

    // Bundle data
    implementation project(path: ':libdtn-common')

    // interface spi
    compile project(path: ':libdtn-core-api')

    // RxJava library for reactive programming
    implementation 'io.reactivex.rxjava2:rxjava:2.2.3'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
